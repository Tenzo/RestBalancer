group 'com.balancer.rest'
version '0.1'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'jetty'
apply plugin: "idea"
apply plugin: 'war'


sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.14'
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.14'
    compile 'log4j:log4j:1.2.17'
    compile "org.yaml:snakeyaml:1.10"
    testCompile 'junit:junit:4.12'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.3'
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

test {
    exclude '**/*IntegrationTest*'
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = false
    }
    outputs.upToDateWhen{false}
}


[jettyRun, jettyRunWar,jettyStop]*.stopPort = 8081
[jettyRun, jettyRunWar,jettyStop]*.stopKey = 'stopKey'

task integrationTest(type: Test, dependsOn: "test") {
    description = 'Runs integration tests'
    include '**/*IntegrationTest*'


    doFirst {
        println"### STARTING JETTY SERVER ###"
        jettyRun.httpPort = 8080    // Port for test
        jettyRun.daemon = true
        jettyRun.execute()
    }

    doLast {
        println"### STOPPING JETTY SERVER ###"
        jettyStop.execute()
    }

    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}